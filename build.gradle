buildscript {
    ext {
        kotlin_version = '1.3.21'
        kotlin_frontend_version = '0.0.45'
        kotlinx_coroutines_version = '1.1.1'
    }

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:$kotlin_frontend_version"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$kotlinx_coroutines_version"
}

compileKotlin2Js {
    kotlinOptions {
        moduleKind = 'commonjs'
        sourceMap = true
    }
}

kotlinFrontend {
    npm {
        dependency 'webextension-polyfill'
    }
    webpackBundle {
        bundleName = 'popup'
        contentPath = file('src/main/web')
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.2.1'
}

task copyWeb(type: Copy) {
    from 'src/main/web'
    into 'build/extension'
}

task copyBrowserPolyfill(type: Copy) {
    dependsOn 'bundle'
    from ('build/node_modules/webextension-polyfill/dist') {
        include 'browser-polyfill.min.js'
    }
    into 'build/extension'
}

task copyBundle(type: Copy) {
    dependsOn 'bundle'
    from ('build/bundle') {
        rename '(.*)\\.bundle.js', '$1.js'
    }
    into 'build/extension'
}

task prepareExtension {
    dependsOn copyWeb, copyBundle, copyBrowserPolyfill
}

task zipExtension(type: Zip) {
    dependsOn prepareExtension
    from 'build/extension'
}

task extension {
    dependsOn zipExtension
}

assemble.dependsOn extension

// TODO remove once https://github.com/Kotlin/kotlin-frontend-plugin/issues/141 is fixed
def fixTask = tasks.register('webpack-config-fix') {
    def dir = file('webpack.config.d')
    it.outputs.dir(dir)
    it.doLast { dir.mkdir() }
}
afterEvaluate {
    tasks.named('webpack-config').configure {
        it.dependsOn(fixTask)
    }
}
